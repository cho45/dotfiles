#!/usr/bin/env perl
use strict;
use warnings;

use opts;
use Perl6::Say;
use File::Temp ();
use Path::Class;
use Mac::FSEvents;
use Term::ANSIColor;
use Twiggy::Server;
use Plack::App::File;
use Test::TCP;

opts my $app => { isa => 'Str', default => 'Firefox' };
my $target = file(shift @ARGV)->absolute;
exit 1 unless -f $target;
my $name   = $target->basename;
my $html   = do { local $/; <DATA> };
$html =~ s{\$name}{$name};

my $port = Test::TCP::empty_port();
my $server = Twiggy::Server->new(
	host => '127.0.0.1',
	port => $port,
);

$server->register_service(sub {
	my $env = shift;
	my $handler = {
		'/' => sub {
			[200, [ 'Content-Type' => 'text/html' ], [ $html ] ];
		},
		"/$name" => sub {
			Plack::App::File->new(file => $target)->to_app->($env);
		},
	}->{$env->{PATH_INFO}};

	$handler ? $handler->() : [404, ['Content-Type' => 'text/plain'], ['not found']];
});


if ($app eq 'Firefox') {
	system('open', "http://127.0.0.1:$port/");
} else {
	system('open', '-a', "http://127.0.0.1:$port/");
}

my $fs = Mac::FSEvents->new({
	path => "$ENV{HOME}/Library/Application Support/Firefox/Profiles/",
});

my $cv = AnyEvent->condvar;
my $watch; $watch = AnyEvent->io(
	fh   => $fs->watch,
	poll => 'r',
	cb   => sub {
		for my $event ($fs->read_events) {
			next unless $event->path =~ qr{gm_scripts/[^/]+/};
			my $installed = [ grep { /\.user\.js$/ } dir($event->path)->children ]->[0];
			if ($installed) {
				$fs->stop;
				undef $watch;
				$cv->send($installed);
			};
		}
	},
);

my $installed = $cv->recv;

if ($installed) {
	say "Notice event:";
	say colored ['green'], $target;
	say "is installed as";
	say colored ['green'], $installed;
	say "\n";
	say "Replace";
	say colored ['green'], $installed;
	say "to symlink of";
	say colored ['green'], $target;
	system('ln', '-sf', $target, $installed);
	say "Done";
	exit 0;
} else {
	exit 1;
}

__END__
<!DOCTYPE html>
<html>
	<head>
		<title>Install User Script</title>
		<script type="text/javascript">
			location.href = '$name';
		</script>
		<style>
			html, body {
				background: #fff;
			}
			#message {
				background: #efefef;
				padding: 1em;
				font-size: 300%;
				color: #666;
				font-weight: bold;
				text-align: center;
			}
			a:link,
			a:visited,
			a:hover {
				text-decoration: none;
				color: #666;
			}
		</style>
	</head>
	<body>
		<p id="message"><a href="$name">Complete the install progress<br/>and close this page.</a></p>
		<script type="text/javascript">
			window.close();
		</script>
	</body>
</html>
